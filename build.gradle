buildscript {
    ext.kotlin_version = '1.5.21'
    ext.ktor_version = '1.6.2'

    ext.h2_version = '1.4.199'
    ext.postgres_version = '42.2.23.jre7'
    ext.exposed_version = '0.32.1'
    ext.hikari_version = '5.0.0'
    ext.flyway_version = '8.2.0'

    ext.flexmark_version = '0.62.2'
    ext.handlebars_version = '4.2.0'
    ext.commons_email_version = '1.5'
    ext.logback_version = '1.2.5'
    ext.konf_version = '1.1.2'
    ext.commonslang_version = '3.12.0'
    ext.crux_version = '2.2.0'
    ext.jsoup_version = '1.9.2'
    ext.cdp4j_version = '3.0.15'
    ext.skedule_version = '0.4.0'

    ext.rest_assured_version = '4.4.0'
    ext.junit_version = '5.7.2'
    ext.assertj_version = '3.20.2'
    ext.mockk_version = '1.12.0'
    ext.wiremock_version = '2.29.1'
}

plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
}

sourceSets {
    testIntegration {
        java.srcDir 'src/testIntegration/java'
        kotlin.srcDir 'src/testIntegration/kotlin'
        resources.srcDir 'src/testIntegration/resources'
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}
configurations {
    testIntegrationImplementation.extendsFrom testImplementation
    testIntegrationRuntime.extendsFrom testRuntime
}
task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

compileTestKotlin {
    kotlinOptions {
        suppressWarnings = true
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    systemProperty "CONFIG_MODE", "TEST"
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

application {
    mainClass = "MainKt"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"

    implementation "com.h2database:h2:$h2_version"
    implementation "org.postgresql:postgresql:$postgres_version"
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "com.zaxxer:HikariCP:$hikari_version"
    implementation "org.flywaydb:flyway-core:$flyway_version"

    implementation "com.vladsch.flexmark:flexmark:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-tasklist:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-tables:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-autolink:$flexmark_version"

    implementation "com.github.shyiko.skedule:skedule:$skedule_version"
    implementation "com.github.jknack:handlebars:$handlebars_version"
    implementation "org.apache.commons:commons-email:$commons_email_version"

    implementation "org.apache.commons:commons-lang3:$commonslang_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "com.uchuhimo:konf-core:$konf_version"

    testImplementation "io.rest-assured:rest-assured:$rest_assured_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremock_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}
