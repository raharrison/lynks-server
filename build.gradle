buildscript {
    ext.kotlin_version = '1.2.50'
    ext.ktor_version = '0.9.3'
    ext.exposed_version = '0.10.3'
    ext.h2_version = '1.4.197'
    ext.flexmark_version = '0.34.0'
    ext.fuel_version = '1.14.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

sourceSets {
    testIntegration {
        java.srcDir 'src/test-integration/java'
        kotlin.srcDir 'src/test-integration/kotlin'
        resources.srcDir 'src/test-integration/resources'
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}
configurations {
    provided
    testCompile.extendsFrom provided
    testIntegrationCompile.extendsFrom testCompile
    testIntegrationRuntime.extendsFrom testRuntime
}
task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

idea {
    module {
        testSourceDirs += project.sourceSets.testIntegration.java.srcDirs
        testSourceDirs += project.sourceSets.testIntegration.kotlin.srcDirs
        testSourceDirs += project.sourceSets.testIntegration.resources.srcDirs
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url = "https://dl.bintray.com/kotlin/ktor" }
    maven { url = "https://dl.bintray.com/kotlin/exposed" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"

    compile "com.h2database:h2:$h2_version"
    compile "org.jetbrains.exposed:exposed:$exposed_version"
    compile "com.zaxxer:HikariCP:3.2.0"

    compile "com.vladsch.flexmark:flexmark:$flexmark_version"
    compile "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:$flexmark_version"
    compile "com.vladsch.flexmark:flexmark-ext-tables:$flexmark_version"
    compile "com.vladsch.flexmark:flexmark-ext-autolink:$flexmark_version"
    compile "com.vladsch.flexmark:flexmark-ext-superscript:$flexmark_version"

    compile "io.webfolder:cdp4j:3.0.0"
    compile "com.github.chimbori:crux:2.0.1"
    compile "com.google.guava:guava:25.1-jre"
    compile 'com.github.shyiko.skedule:skedule:0.3.0'
    compile "com.github.kittinunf.fuel:fuel:$fuel_version"
    compile "com.github.kittinunf.fuel:fuel-coroutines:$fuel_version"

    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "com.uchuhimo:konf:0.10"

    testCompile "io.rest-assured:rest-assured:3.1.0"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testCompile "org.assertj:assertj-core:3.10.0"
    testCompile "io.mockk:mockk:1.8.4"
}
