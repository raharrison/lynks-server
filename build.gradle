buildscript {
    ext.kotlin_version = '1.6.10'
    ext.ktor_version = '1.6.7'

    ext.h2_version = '2.1.210'
    ext.postgres_version = '42.3.1'
    ext.exposed_version = '0.37.3'
    ext.hikari_version = '5.0.1'
    ext.flyway_version = '8.5.0'

    ext.flexmark_version = '0.64.0'
    ext.handlebars_version = '4.3.0'
    ext.commons_email_version = '1.5'
    ext.logback_version = '1.2.10'
    ext.konf_version = '1.1.2'
    ext.commonslang_version = '3.12.0'
    ext.skedule_version = '0.4.0'

    ext.kotlinx_coroutines_test_version = '1.6.0'
    ext.rest_assured_version = '4.5.0'
    ext.junit_version = '5.8.2'
    ext.assertj_version = '3.22.0'
    ext.mockk_version = '1.12.3'
    ext.wiremock_version = '2.32.0'
}

plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

version = "1.1.0"

sourceSets {
    testIntegration {
        java.srcDir 'src/testIntegration/java'
        kotlin.srcDir 'src/testIntegration/kotlin'
        resources.srcDir 'src/testIntegration/resources'
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}
configurations {
    testIntegrationImplementation.extendsFrom testImplementation
    testIntegrationRuntime.extendsFrom testRuntime
}
task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

task createProperties() {
    doLast {
        def file = new File("$buildDir/resources/main/version.txt")
        file.getParentFile().mkdirs()
        file.text = project.version.toString()
    }
}
classes {
    dependsOn createProperties
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    systemProperty "CONFIG_MODE", "TEST"
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

application {
    mainClass = "lynks.MainKt"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"

    implementation "com.h2database:h2:$h2_version"
    implementation "org.postgresql:postgresql:$postgres_version"
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "com.zaxxer:HikariCP:$hikari_version"
    implementation "org.flywaydb:flyway-core:$flyway_version"

    implementation "com.vladsch.flexmark:flexmark:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-tasklist:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-tables:$flexmark_version"
    implementation "com.vladsch.flexmark:flexmark-ext-autolink:$flexmark_version"

    implementation "com.github.shyiko.skedule:skedule:$skedule_version"
    implementation "com.github.jknack:handlebars:$handlebars_version"
    implementation "org.apache.commons:commons-email:$commons_email_version"

    implementation "org.apache.commons:commons-lang3:$commonslang_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "com.uchuhimo:konf-core:$konf_version"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines_test_version"
    testImplementation "io.rest-assured:rest-assured:$rest_assured_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremock_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}
